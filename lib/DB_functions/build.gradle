def env = System.getenv()
def dbname = env['DBNAME']
def pgbindir = env['PGBINDIR']

task deployPostgresFunctions {
    description = 'Deploy postgres functions'

    doLast {
        println 'deploying postgres functions ...'
        int numberOfFunctions = runSqlFiles('.')
        println "Deployed functions: ${numberOfFunctions}"
    }
}

int runSqlFiles(String directory) {
    int numberOfProc = 0

    file(directory).listFiles().sort().each { File file ->
        if (file.name.endsWith('.sql')) {
            exec {
                commandLine pgbindir + '/psql',
                        '-d', dbname,
                        '-q',
                        '-v', 'ON_ERROR_STOP=1',
                        '-v', 'client_min_messages=warning',
                        '-f', file.absolutePath
            }
            numberOfProc++
        }
        if (file.isDirectory()) {
            int number = runSqlFiles(file.absolutePath)
            println "deployed ${number} files in subdirectory: ${file.name}"
        }
    }
    return numberOfProc
}

task dropFunctions {
    description = 'Drop all functions from the database'
    doLast {
        exec { commandLine 'bash', '-c', "psql -v ON_ERROR_STOP=1 ${dbname} < ./dropFunctions" }
    }
}
