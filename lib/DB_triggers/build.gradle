def env = System.getenv()
def dbname = env['DBNAME']
def pgbindir = env['PGBINDIR']

task deployPostgresTriggers {
    description = 'Deploy postgres triggers'
    doLast {
        println 'deploying postgres triggers ...'
        int numberOfTriggers = runSqlFiles('.')
        println "Deployed triggers: ${numberOfTriggers}"
    }
}

int runSqlFiles(String directory) {
    int numberOfTriggers = 0

    file(directory).listFiles({ file -> file.isFile() } as FileFilter).sort().each { File file ->
        if (file.name.endsWith('.sql')) {
            exec {
                commandLine pgbindir + '/psql',
                        '-d', dbname,
                        '-q',
                        '-v', 'ON_ERROR_STOP=1',
                        '-v', 'client_min_messages=warning',
                        '-f', file.absolutePath
            }
            numberOfTriggers++
        }
    }
    return numberOfTriggers
}

task dropTriggers {
    description = 'Drop all triggers from the database'
    doLast {
        exec { commandLine 'bash', '-c', "echo 'select strip_all_triggers()' | psql -v ON_ERROR_STOP=1 ${dbname}" }
    }
}
