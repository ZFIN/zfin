buildscript {
    dependencies {
        classpath 'org.postgresql:postgresql:42.2.20'
    }
}

plugins {
    id 'war'
    id 'application'
    id 'org.liquibase.gradle' version '2.2.2'
}

group = 'org.zfin'
version = '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'groovy'
apply from: 'agr.gradle'
apply from: 'console.gradle'

sourceCompatibility = 17
targetCompatibility = 17

// Application plugin configuration for command-line utilities
application {
    mainClass = 'org.zfin.util.commandline.CommandLineUtilityRunner'
    applicationName = 'zfin-util'
    applicationDefaultJvmArgs = ['-Xmx512m']
}

def env = System.getenv()
def dbname = env['DBNAME']
def sourceroot = env['SOURCEROOT']
def pgbindir = env['PGBINDIR']
def pgdata = env['PGDATA']
def pghost = env['PGHOST']
def dbunloadspath = env['DB_UNLOADS_PATH']
def solrunloadspath = env['SOLR_UNLOADS_PATH']
def downloaddirectory = env['DOWNLOAD_DIRECTORY']
String targetroot = env['TARGETROOT']


task deployPostgres(dependsOn: ['deployPostgresFunctions',
                                'deployPostgresTrigger']) {
    description "Deploy all postgres code"
}

defaultTasks 'make'

project.ext.set("dbname", dbname)
project.ext.set("sourceroot", sourceroot)
project.ext.set("pgdata", pgdata)
project.ext.set("pgbindir", pgbindir)
project.ext.set("targetroot", targetroot)

// Define version variables for consistency
project.ext.set("jacksonVersion", '2.15.2')

def localFileDependencies = [
        "home/WEB-INF/lib/agr_curation_api.jar",
        "home/WEB-INF/lib/bbop.jar",
        "home/WEB-INF/lib/biojava.1.7.1.jar",
        "home/WEB-INF/lib/blast-serialization-1.0-eclipse-transformed.jar",
        "home/WEB-INF/lib/commons-configuration-ant-task-0.9.7.jar",
        "home/WEB-INF/lib/cvu.jar",
        "home/WEB-INF/lib/gwt-servlet-jakarta-2.11.0.jar",
        "home/WEB-INF/lib/jakarta.servlet.jsp.jstl-api-1.2.7-eclipse-transformed.jar",
        "home/WEB-INF/lib/obo.jar",
        "home/WEB-INF/lib/patricia-trie-0.2.jar",  // Not in Maven Central (0.6 version maybe "org.ardverk:patricia-trie:0.6",
        "home/WEB-INF/lib/rescu-2.1.0-eclipse-transformed.jar",
        "home/WEB-INF/lib/restygwt-2.2.7-eclipse-transformed.jar",
        "home/WEB-INF/lib/taglibs-standard-impl-1.2.5-eclipse-transformed.jar",
        "home/WEB-INF/lib/text-table-formatter-1.0.jar",
        "lib/Java/gwt/gwt-user-2.11.0.jar",
        "lib/Java/gwt/jsinterop-annotations-2.0.0.jar"
];

// ============================================================================
// DEPENDENCY RESOLUTION STRATEGY (Conflict Resolution)
// ============================================================================
configurations.all {
    // Exclude XML parsers provided by Tomcat from all dependencies
    exclude group: 'xpp3', module: 'xpp3'
    exclude group: 'pull-parser', module: 'pull-parser'
    exclude group: 'xerces', module: 'xercesImpl'
    exclude group: 'xalan', module: 'xalan'
    exclude group: 'xml-apis', module: 'xml-apis'

    resolutionStrategy {
        // Force consistent Jackson version across all dependencies to avoid NoSuchMethodError
        eachDependency { details ->
            if (details.requested.group.startsWith("com.fasterxml.jackson")) {
                details.useVersion(jacksonVersion)
                details.because("Force consistent Jackson version to avoid BufferRecycler.releaseToPool() incompatibility")
            }
        }
    }
}

task loadDatabase() {
    doLast {
        File unloads = file(dbunloadspath)
        String unloadPath
        if (unloads.exists()) {
            def files = unloads.listFiles()
            Arrays.sort(files, new Comparator<File>() {
                int compare(File f1, File f2) {
                    return Long.valueOf(f2.lastModified()).compareTo(f1.lastModified())
                }
            })
            File latest = files[0]
            File latestBakDump = latest.listFiles().last()
            unloadPath = latestBakDump.getPath()
        }

        //use -DB=... path if specified, also, yes, I'm being cute so that it will be -DB=...
        //rather than -Ddb=...
        if (System.getProperty("B")) {
            unloadPath = System.getProperty("B")
        }

        //might as well support -Dunload also
        if (System.getProperty("unload")) {
            unloadPath = System.getProperty("unload")
        }

        println "Loading $unloadPath into $dbname"
        exec {
            ignoreExitValue = true
            commandLine "bash", "-c", "echo 'select pg_terminate_backend(pg_stat_activity.pid) from pg_stat_activity where pid <> pg_backend_pid();' | psql -v ON_ERROR_STOP=1 $dbname"
        }
        exec {
            ignoreExitValue = true
            commandLine 'dropdb', dbname
        }
        exec {
            ignoreExitValue = true
            commandLine 'createdb', dbname
        }
        exec {
            ignoreExitValue = true
            commandLine "bash", "-c", "pg_restore -j 8 -d $dbname $unloadPath"
        }
        exec {
            ignoreExitValue = true
            commandLine "bash", "-c", "echo 'vacuum (analyze)' | psql -v ON_ERROR_STOP=1 $dbname"
        }
    }
}

task loadDb(type: GradleBuild) {
    tasks = ['loadDatabase']
}

task downloadDB(type: GradleBuild) {
    tasks = ['getLatestDatabaseUnload']
}

task downloadSolrIndex(type: GradleBuild) {
    tasks = ['getLatestSolrUnload']
}

task loadSolrIndex(type: GradleBuild) {
    tasks = ['getLatestSolrIndex']
}

task getdb(dependsOn: downloadDB) //alias for downloadDB (aka getLatestDatabaseUnload)
task loaddb(dependsOn: loadDb) //just an alias, since we're used to loaddb.pl


task buildDatabase(type: GradleBuild) {
    tasks = ['liquibasePreBuild']
}

task buildPostGmakeDatabase(type: GradleBuild) {
    tasks = ['liquibasePostBuild']
}

task reload(type: GradleBuild) {
    tasks = ['loadDatabase', 'liquibasePreBuild', 'liquibasePostBuild']
}

String preChangeLog = "source/org/zfin/db/load/db.changelog.master.xml"
String postChangeLog = "source/org/zfin/db/postGmakePostloaddb/db.changelog.master.xml"

// Configure liquibase activities at configuration time (required for Gradle 8+)
liquibase {
    activities {
        preBuild {
            changelogFile preChangeLog
            url 'jdbc:postgresql://' + pghost + ':5432/' + dbname
        }
        postBuild {
            changelogFile postChangeLog
            url 'jdbc:postgresql://' + pghost + ':5432/' + dbname
        }
    }
}

// Create custom tasks that configure and run liquibase update
task liquibasePreBuild {
    group = 'liquibase'
    description = 'Run liquibase update for pre-build changesets'

    doFirst {
        println "Running liquibase preBuild activity with changelog: ${preChangeLog}"
        // Set the runList before update task executes
        project.liquibase.runList = 'preBuild'
    }
}

task liquibasePostBuild {
    group = 'liquibase'
    description = 'Run liquibase update for post-build changesets'

    doFirst {
        println "Running liquibase postBuild activity with changelog: ${postChangeLog}"
        // Set the runList before update task executes
        project.liquibase.runList = 'postBuild'
    }
}

// Make the update task run after configuration
liquibasePreBuild.finalizedBy('update')
liquibasePostBuild.finalizedBy('update')

// variables used for translation of keys in files in SOURCEROOT copied into TARGETROOT
def translateVariableSet = [
        'CATALINA_HOME',
        'CGI_BIN_DIR_NAME',
        'DB_NAME',
        'PGHOST',
        'DEFAULT_EMAIL',
        'ENVIRONMENT',
        'FTP_ROOT',
        'GBROWSE_PATH_FROM_ROOT',
        'GENBANK_DAILY_EMAIL',
        'GO_EMAIL_ERR',
        'GO_EMAIL_CURATOR',
        'GROOVY_CLASSPATH',
        'INSTANCE',
        'JAVA_HOME',
        'JBROWSE_PATH_FROM_ROOT',
        'LOADUP_FULL_PATH',
        'MOVE_BLAST_FILES_TO_DEVELOPMENT',
        'MUTANT_NAME',
        'PARTNER_DBNAME',
        'PARTNER_INTERNAL_INSTANCE',
        'ROOT_PATH',
        'SHARED_DOMAIN_NAME',
        'SOURCEROOT',
        'SQLHOSTS_FILE',
        'SWISSPROT_EMAIL_REPORT',
        'SWISSPROT_EMAIL_CURATOR',
        'SWISSPROT_EMAIL_ERR',
        'TARGETROOT',
        'USER',
        'VALIDATION_EMAIL_DBA',
        'WEBHOST_BLASTDB_TO_COPY',
        'WEBHOST_FASTA_FILE_PATH',
]

// load properties into gradle
task setup {
    project.ext.ttNameMap = new Properties()
    file("home/WEB-INF/zfin.properties").withInputStream {
        ttNameMap.load(it)
    }
    Map filteredMap = new HashMap()
    project.ext.ttNameMap.each { name, value ->
        if (translateVariableSet.contains(name)) {
            filteredMap.put(name, value)
        }
    }
    project.ext.ttNameMap = filteredMap
}

task printProp() {
    doLast {
        println 'All Properties'
        ttNameMap.each { name, value ->
            println name + ' = ' + value
        }
    }
}

task 'home;root;deployFiles' {
    doLast {
        def sourceFiles = [file('home/robots.txt'), file('home/favicon.ico')]
        def targetDir = file("${targetroot}/home")

        if (!targetDir.exists()) {
            targetDir.mkdirs()
        }

        sourceFiles.each { sourceFile ->
            def targetFile = new File(targetDir, sourceFile.name)
            targetFile.bytes = sourceFile.bytes
        }

        println "Copied ${sourceFiles.size()} files to ${targetDir}"
    }
}

task "home;zf_info;deployFiles" (type: SimpleDirectoryCopyTask) {
    sourcePath = 'home/zf_info'
    excludes = ['Makefile']
}

task "home;ZFIN;deployFiles" (type: SimpleDirectoryCopyTask) {
    sourcePath = 'home/ZFIN'
    excludes = ['Makefile']
}

task "server_apps;sysexecs;deployFiles" (type: SimpleDirectoryCopyTask) {
    sourcePath = 'server_apps/sysexecs'
    excludes = ['Makefile', 'build.gradle']
}

task "server_apps;apache;deployFiles" (type: SimpleDirectoryCopyTask) {
    sourcePath = 'server_apps/apache'
    excludes = ['Makefile', 'build.gradle']
}

task "server_apps;Reports;deployFiles" (type: SimpleDirectoryCopyTask) {
    sourcePath = 'server_apps/Reports'
    excludes = ['Makefile', 'build.gradle']
}

task "server_apps;tokens;deployFiles" (type: SimpleDirectoryCopyTask) {
    sourcePath = 'server_apps/tokens'
}

task "server_apps;data_transfer;BLAST;deployFiles" (type: SimpleDirectoryCopyTask) {
    sourcePath = 'server_apps/data_transfer/BLAST'
    excludes = ['Danio_rerio.GRCz10.pep.all.fa.gz', 'Danio_rerio.GRCz11.pep.all.fa.gz', 'Danio_rerio.Zv9.pep.all.fa.gz',
        'zfin_genomic_dna_all.fa', 'GenomicDNA.xnt', 'GenomicDNA.xni', 'zfin_genomic_genbank_acc.unl', 'GenomicDNA.xnd',
        'GenomicDNA.xns']
}

task "server_apps;DB_maintenance;deployFiles" (type: SimpleDirectoryCopyTask) {
    sourcePath = 'server_apps/DB_maintenance'
    excludes = ['Makefile', 'build.gradle']
    excludeDirs = ['postgres/liquibase']
}

task "cgi-bin;deployFiles" (type: SimpleDirectoryCopyTask) {
    sourcePath = 'cgi-bin'
    excludes = ['Makefile', 'build.gradle']
}

task "server_apps;perl_lib;deployFiles" (type: SimpleDirectoryCopyTask) {
    sourcePath = 'server_apps/perl_lib'
}

task deployGitInfoFile() {
    doLast {
        def commitNumber = 'git rev-parse HEAD'.execute().text.trim()
        def releaseNumber = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
        println "Deploying release commit number: " + commitNumber + " and release number: " + releaseNumber + " into zdb_property table."

        def sqlCommandCommit = "INSERT INTO zdb_property (zprop_name, zprop_value) VALUES ('RELEASE_COMMIT_HASH', '" + commitNumber + "') ON CONFLICT (zprop_name) DO UPDATE SET zprop_value = EXCLUDED.zprop_value;"
        def sqlCommandRelease = "INSERT INTO zdb_property (zprop_name, zprop_value) VALUES ('RELEASE_NUMBER', '" + releaseNumber + "') ON CONFLICT (zprop_name) DO UPDATE SET zprop_value = EXCLUDED.zprop_value;"

        exec {
            commandLine "bash", "-c", "echo \"" + sqlCommandCommit + "\" | psql -v ON_ERROR_STOP=1 " + dbname
        }
        exec {
            commandLine "bash", "-c", "echo \"" + sqlCommandRelease + "\" | psql -v ON_ERROR_STOP=1 " + dbname
        }
    }
}

task "server_apps;data_transfer;deployFiles" (type: SimpleDirectoryCopyTask) {
    sourcePath = 'server_apps/data_transfer'
    excludes = ['Makefile', 'build.gradle', '.gitignore', '.DS_Store']
    excludeDirs = ['SangerMutants', 'Ensembl', 'BLAST']
}

task "server_apps;data_transfer;Ensembl;deployFiles" (type: SimpleDirectoryCopyTask) {
    sourcePath = 'server_apps/data_transfer/Ensembl'
    includes = ['sh', 'sql', 'groovy']
}

task "server_apps;data_transfer;Downloads;createSymlink"() {
    doLast {
        if (!file(downloaddirectory).exists()) {
            exec {
                commandLine "mkdir", "-p", downloaddirectory
            }
        }
        String datatransferpath = targetroot + "/home/data-transfer"
        println "Creating symlink from " + downloaddirectory + " to " + datatransferpath
        if (!file(datatransferpath).exists()) {
            exec {
                commandLine "ln", "-s", downloaddirectory, datatransferpath
            }
        }
    }
}

task make(type: GradleBuild) {
    tasks = ['deployPostgresFunctions',
             'deployPostgresTriggers',
             'createEmptyDirsIfNotExists',
             'home;root;deployFiles',
             'home;zf_info;deployFiles',
             'home;ZFIN;deployFiles',
             'server_apps;apache;deployFiles',
             'server_apps;sysexecs;deployFiles',
             'server_apps;DB_maintenance;deployFiles',
             'server_apps;data_transfer;deployFiles',
             'server_apps;data_transfer;Downloads;createSymlink',
             'server_apps;data_transfer;Ensembl;deployFiles',
             'server_apps;Reports;deployFiles',
             'server_apps;tokens;deployFiles',
             'server_apps;data_transfer;BLAST;deployFiles',
             'deployGitInfoFile',
             'cgi-bin;deployFiles',
             'server_apps;perl_lib;deployFiles',
    ]
}

task createEmptyDirsIfNotExists() {
    doLast {
        exec {
            commandLine "mkdir", "-p", targetroot + "/home/data_transfer/Downloads/"
        }
        exec {
            commandLine "mkdir", "-p", targetroot + "/server_apps/data_transfer/Downloads/GFF3/knockdown_reagents/"
        }
        exec {
            commandLine "mkdir", "-p", downloaddirectory + "/current/"
        }
    }
}

task getLatestSolrUnload() {
    doLast {
        if (solrunloadspath != '/research/zunloads/solr/zfindb') {
            def command = "ssh " + env['SSH_USER'] + "@" + env['SSH_HOST'] + " " + 'ls -d /research/zunloads/solr/zfindb/20??.??.??-??:?? | sort -n |tail -n 1'
            def filepath = command.execute().text.trim()
            println "Getting " + filepath
            exec {
                ignoreExitValue = true
                commandLine "scp", "-r", env['SSH_USER'] + "@" + env['SSH_HOST'] + ":" + filepath, solrunloadspath
            }
        } else {
            println 'SOLR_UNLOADS_PATH is set to ' + solrunloadspath + '. Please change before attempting to copy.'
        }
    }
}

task getLatestSolrIndexFilesTrunk() {
    doLast {
        exec {
            ignoreExitValue = true
            commandLine "bash", "-c", "scp -r " + env['SSH_USER'] + "@" + env['SSH_HOST'] + ":/research/zunloads/solr/trunkdb/current current.solr.dump"
        }
    }
}

task getLatestDatabaseUnload() {
    doLast {
        if (dbunloadspath != '/research/zunloads/databases/zfindb') {
            def command = "ssh " + env['SSH_USER'] + "@" + env['SSH_HOST'] + " " + 'ls -d /research/zunloads/databases/zfindb/20??.??.??.? | sort -n |tail -n 1'
            def filepath = command.execute().text.trim()
            println "Getting " + filepath
            exec {
                ignoreExitValue = true
                commandLine "scp", "-r", env['SSH_USER'] + "@" + env['SSH_HOST'] + ":"  + filepath, dbunloadspath
            }
        } else {
            println 'DB_UNLOADS_PATH is set to ' + dbunloadspath + '. Please change before attempting to copy.'
        }
    }
}

task getLatestPostgresFilesTrunk() {
    doLast {
        exec {
            ignoreExitValue = true
            commandLine "bash", "-c", "scp " + env['SSH_USER'] + "@" + env['SSH_HOST'] + ":/research/zunloads/databases/trunkdb/current current.postgres.dump"
        }
    }
}

task getLatestSolrIndex() {
    doLast {
        println 'Upgrade Solr index to the latest production one...'
        println "latest index: " + "find ./* -type d -prune -exec ls -d ".execute().text

        def folder = solrunloadspath
        def baseDir = new File(folder)
        def files = baseDir.listFiles()
        Arrays.sort(files, new Comparator<File>() {
            int compare(File f1, File f2) {
                return Long.valueOf(f2.lastModified()).compareTo(f1.lastModified())
            }
        })
        def latestIndex = files[0]
        println latestIndex
        def sout = new StringBuilder(), serr = new StringBuilder()
        def procString = "ant restore-solr-core -DRESTORE_FROM=" + latestIndex.getAbsolutePath()
        def proc = procString.execute()
        def b = new StringBuffer()
        proc.consumeProcessErrorStream(b)

        println proc.text
        println b.toString()

    }
}

//getsolr as alias for getLatestSolrUnload
task getsolr(dependsOn: getLatestSolrUnload)

//loadsolr as alias for getLatestSolrIndex
task loadsolr(dependsOn: getLatestSolrIndex)

task getBlastAllDatabases() {
    doLast {
        exec {
            ignoreExitValue = true
            commandLine "rsync", "-av", env['SSH_USER'] + "@" + env['SSH_HOST'] + ":/research/zfin.org/blastdb/Current", "/opt/zfin/blastdb/"
        }
    }
}

task getBlastSmallDatabases() {
    doLast {
        exec {
            ignoreExitValue = true
            commandLine "rsync", "-av", "--max-size=100m", env['SSH_USER'] + "@" + env['SSH_HOST'] + ":/research/zfin.org/blastdb/Current", "/opt/zfin/blastdb/"
        }
    }
}

task getBlastBinaries() {
    doLast {
        exec {
            ignoreExitValue = true
            commandLine "rsync", "-av", env['SSH_USER'] + "@" + env['SSH_HOST'] + ":/opt/ab-blast", "/opt/zfin/blastdb/"
        }
    }
}

task getBlast(type: GradleBuild) {
    tasks = ['getBlastSmallDatabases',
             'getBlastBinaries'
    ]
}

task createSchemaSpy() {
    doLast {
        println 'Creating ER diagram for ' + ttNameMap.get('DB_NAME') + ' ...'
        javaexec {
            main = "-jar"
            args = [
                    "lib/Java/schemaSpy_5.0.0.jar",
                    "-t",
                    "pgsql",
                    "-db",
                    ttNameMap.get('DB_NAME'),
                    "-u",
                    ttNameMap.get('USER'),
                    "-host",
                    pghost,
                    "-dp",
                    "lib/Java/postgresql-42.2.18.jar",
                    "-s",
                    "public",
                    "-o",
                    ttNameMap.get('TARGETROOT') + "/home/schemaSpy",
            ]
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }  // For GitHub-hosted dependencies
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:1.18.20"
    compileOnly "com.github.nmorel.gwtjackson:gwt-jackson:0.15.4"
    compileOnly "com.google.jsinterop:jsinterop-annotations:2.0.0"
    compileOnly "jakarta.servlet.jsp:jakarta.servlet.jsp-api:3.1.1"
    compileOnly "org.gwtproject:gwt-dev:2.11.0"
    compileOnly "org.projectlombok:lombok:1.18.20"
    compileOnly "xalan:xalan:2.7.0"
    compileOnly "xerces:xercesImpl:2.12.2"
    compileOnly "xml-apis:xml-apis:1.4.01"

    implementation "backport-util-concurrent:backport-util-concurrent:3.1"
    implementation "com.fasterxml:classmate:1.5.1"
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.15.2"
    implementation "com.fasterxml.jackson.core:jackson-core:2.15.2"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.15.2"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.15.2"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.15.2"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-guava:2.15.2"  // Required by robot.jar
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2"
    implementation "com.fasterxml.woodstox:woodstox-core:6.5.1"
    implementation "com.github.rtaylorzfin:google-analytics-reporting-app:1.0.4"
    implementation "com.github.samtools:htsjdk:4.3.0"
    implementation "com.google.code.gson:gson:2.2.4"
    implementation "com.google.guava:guava:27.1-jre"
    implementation "com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20200713.1"
    implementation "com.mchange:c3p0:0.9.5.5"
    implementation "com.mchange:mchange-commons-java:0.2.15"
    implementation "com.sun.activation:jakarta.activation:2.0.1"
    implementation "com.sun.istack:istack-commons-runtime:3.0.7"
    implementation "com.sun.mail:jakarta.mail:2.0.1"
    implementation "com.sun.xml.fastinfoset:FastInfoset:1.2.15"
    implementation "com.sun.xml.messaging.saaj:saaj-impl:3.0.3"
    implementation "com.sun.xml.stream.buffer:streambuffer:1.5.6"
    implementation "com.sun.xml.ws:policy:2.3.1"
    implementation "com.xlson.groovycsv:groovycsv:1.0"
    implementation "commons-beanutils:commons-beanutils:1.9.1"
    implementation "commons-cli:commons-cli:1.4"
    implementation "commons-collections:commons-collections:3.2.1"
    implementation "commons-configuration:commons-configuration:1.6"
    implementation "commons-digester:commons-digester:1.6"
    implementation "commons-discovery:commons-discovery:0.2"
    implementation "commons-lang:commons-lang:2.6"
    implementation "commons-math:commons-math:1.2"
    implementation "commons-net:commons-net:1.4.1"
    implementation "commons-validator:commons-validator:1.1.4"
    implementation "io.micrometer:micrometer-commons:1.12.1"
    implementation "io.micrometer:micrometer-observation"
    implementation "io.netty:netty-all:4.1.106.Final"
    implementation "io.projectreactor:reactor-core:3.6.1"
    implementation "jakarta.annotation:jakarta.annotation-api:2.0.0"

    implementation "jakarta.persistence:jakarta.persistence-api:3.1.0"
    compileOnly "jakarta.servlet:jakarta.servlet-api:6.0.0"
    compileOnly "jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api:3.0.0"
    implementation "jakarta.transaction:jakarta.transaction-api:2.0.0"
    implementation "jakarta.validation:jakarta.validation-api:3.0.2"
    implementation "jakarta.ws.rs:jakarta.ws.rs-api:3.1.0"
    implementation "jakarta.xml.bind:jakarta.xml.bind-api:3.0.1"
    implementation "javax.jws:jsr181-api:1.0-MR1"
    implementation "javax.validation:validation-api:1.0.0.GA"
    implementation "javax.xml.soap:javax.xml.soap-api:1.4.0"
    implementation "javax.xml.ws:jaxws-api:2.2"
    implementation "jaxen:jaxen:1.1.1"
    implementation "net.bytebuddy:byte-buddy:1.12.7"
    implementation "net.logstash.log4j:jsonevent-layout:1.0"
    implementation "net.minidev:json-smart:1.1.1"
    implementation "oauth.signpost:signpost-core:1.2.1.2"
    implementation "org.altcha:altcha:1.1.2"
    implementation "org.antlr:antlr4-runtime:4.13.0"
    implementation "org.apache.ant:ant:1.8.1"
    implementation "org.apache.commons:commons-collections4:4.4"
    implementation "org.apache.commons:commons-csv:1.11.0"
    implementation "org.apache.commons:commons-exec:1.0"
    implementation "org.apache.commons:commons-lang3:3.14.0"
    implementation "org.apache.commons:commons-text:1.11.0"
    implementation "org.apache.geronimo.specs:geronimo-validation_1.0_spec:1.0-CR5"
    implementation "org.apache.groovy:groovy-all:4.0.29"
    implementation "org.apache.groovy:groovy:4.0.29"
    implementation "org.apache.httpcomponents:httpclient:4.5.10"
    implementation "org.apache.httpcomponents:httpcore:4.4.16"
    implementation "org.apache.httpcomponents:httpmime:4.5.10"
    implementation "org.apache.logging.log4j:log4j-1.2-api:2.21.1"
    implementation "org.apache.logging.log4j:log4j-api:2.21.1"
    implementation "org.apache.logging.log4j:log4j-core:2.21.1"
    implementation "org.apache.logging.log4j:log4j-jakarta-web:2.21.1"
    implementation "org.apache.logging.log4j:log4j-jcl:2.21.1"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.21.1"
    implementation "org.apache.lucene:lucene-analyzers-common:8.11.2"
    implementation "org.apache.lucene:lucene-core:9.4.2"
    implementation "org.apache.neethi:neethi:2.0.4"
    implementation "org.apache.poi:poi-ooxml:5.2.5"
    implementation "org.apache.poi:poi:5.2.5"
    implementation "org.apache.solr:solr-solrj:8.11.3"
    implementation "org.apache.ws.commons.axiom:axiom-api:1.2.7"
    implementation "org.apache.ws.commons.axiom:axiom-impl:1.2.7"
    implementation "org.apache.ws.commons.schema:XmlSchema:1.4.2"
    implementation "org.apache.xmlbeans:xmlbeans:2.4.0"
    implementation "org.apache.zookeeper:zookeeper:3.4.6"
    implementation "org.codehaus.castor:castor-core:1.3"
    implementation "org.codehaus.castor:castor-xml:1.3"
    implementation "org.codehaus.woodstox:stax2-api:3.1.4"
    implementation "org.codehaus.woodstox:woodstox-core-asl:4.4.1"
    implementation "org.codehaus.woodstox:wstx-asl:3.2.7"

    implementation("org.dom4j:dom4j:2.1.3") {
        // Exclude xpp3 XML parser - conflicts with Tomcat's built-in parsers
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'pull-parser', module: 'pull-parser'
    }

    implementation "org.eclipse.microprofile.openapi:microprofile-openapi-api:3.1.1"
    implementation "org.freemarker:freemarker:2.3.20"
    implementation "org.glassfish.gmbal:gmbal-api-only:3.2.0-b003"
    implementation "org.glassfish.jaxb:jaxb-core:4.0.3"
    implementation "org.glassfish.jaxb:jaxb-runtime:4.0.3"
    implementation "org.glassfish.jaxb:jaxb-xjc:4.0.1"
    implementation "org.glassfish.jaxb:txw2:4.0.3"
    implementation "org.hibernate.common:hibernate-commons-annotations:6.0.6.Final"
    implementation "org.hibernate.orm:hibernate-c3p0:6.4.4.Final"
    implementation "org.hibernate.orm:hibernate-core:6.4.4.Final"
    implementation "org.hibernate.search:hibernate-search-engine:6.1.1.Final"
    implementation "org.hibernate.search:hibernate-search-mapper-pojo-base:6.1.1.Final"
    implementation "org.hibernate.validator:hibernate-validator:8.0.1.Final"
    implementation "org.imgscalr:imgscalr-lib:4.2"
    implementation "org.javassist:javassist:3.24.1-GA"
    implementation "org.jboss:jandex:2.4.2.Final"
    implementation "org.jboss.logging:jboss-logging-annotations:1.2.0.Beta1"
    implementation "org.jboss.logging:jboss-logging:3.4.3.Final"
    implementation "org.jboss.spec.javax.transaction:jboss-transaction-api_1.2_spec:1.1.1.Final"
    implementation "org.jdom:jdom:1.1"
    implementation "org.jooq:jool:0.9.15"
    implementation "org.json:json:20210307"
    implementation "org.jvnet.staxex:stax-ex:1.8"
    implementation "org.liquibase:liquibase-core:4.7.1"
    implementation "org.noggit:noggit:0.7"
    implementation "org.ow2.asm:asm-analysis:5.0.3"
    implementation "org.ow2.asm:asm-commons:5.0.3"
    implementation "org.ow2.asm:asm-tree:5.0.3"
    implementation "org.ow2.asm:asm-util:5.0.3"
    implementation "org.ow2.asm:asm-xml:5.0.3"
    implementation "org.ow2.asm:asm:5.0.3"
    implementation "org.postgresql:postgresql:42.2.20"
    implementation "org.projectlombok:lombok:1.18.20"
    implementation "org.reactivestreams:reactive-streams:1.0.4"
    implementation "org.slf4j:jcl-over-slf4j:1.7.26"
    implementation "org.slf4j:jul-to-slf4j:2.0.12"
    implementation "org.slf4j:slf4j-api:1.7.26"
    implementation "org.springframework:spring-aop:6.1.1"
    implementation "org.springframework:spring-aspects:6.1.1"
    implementation "org.springframework:spring-beans:6.1.1"
    implementation "org.springframework:spring-context-support:6.1.1"
    implementation "org.springframework:spring-context:6.1.1"
    implementation "org.springframework:spring-core:6.1.1"
    implementation "org.springframework:spring-expression:6.1.1"
    implementation "org.springframework:spring-jcl:6.1.1"
    implementation "org.springframework:spring-jdbc:6.1.1"
    implementation "org.springframework:spring-jms:6.1.1"
    implementation "org.springframework:spring-messaging:6.1.1"
    implementation "org.springframework:spring-orm:6.1.1"
    implementation "org.springframework:spring-oxm:6.1.1"
    implementation "org.springframework:spring-test:6.1.1"
    implementation "org.springframework:spring-tx:6.1.1"
    implementation "org.springframework:spring-web:6.1.1"
    implementation "org.springframework:spring-webflux:6.1.1"
    implementation "org.springframework:spring-webmvc:6.1.1"
    implementation "org.springframework.batch:spring-batch-core:5.1.1"
    implementation "org.springframework.batch:spring-batch-infrastructure:5.1.1"
    implementation "org.springframework.data:spring-data-commons:3.2.2"
    implementation "org.springframework.data:spring-data-jpa:3.2.2"
    implementation "org.springframework.integration:spring-integration-core:6.2.1"
    implementation "org.springframework.ldap:spring-ldap-core:3.1.1"
    implementation "org.springframework.retry:spring-retry:2.0.5"
    implementation "org.springframework.security:spring-security-acl:6.1.8"
    implementation "org.springframework.security:spring-security-aspects:6.1.8"
    implementation "org.springframework.security:spring-security-cas:6.1.8"
    implementation "org.springframework.security:spring-security-config:6.1.8"
    implementation "org.springframework.security:spring-security-core:6.1.8"
    implementation "org.springframework.security:spring-security-crypto:6.1.8"
    implementation "org.springframework.security:spring-security-ldap:6.1.8"
    implementation "org.springframework.security:spring-security-messaging:6.1.8"
    implementation "org.springframework.security:spring-security-oauth2-client:6.1.8"
    implementation "org.springframework.security:spring-security-oauth2-jose:6.1.8"
    implementation "org.springframework.security:spring-security-taglibs:6.1.8"
    implementation "org.springframework.security:spring-security-web:6.1.8"
    implementation "org.springframework.ws:spring-ws-core:4.0.10"
    implementation "org.springframework.ws:spring-xml:2.0.5.RELEASE"
    implementation "org.yaml:snakeyaml:2.2"
    implementation "oro:oro:2.0.8"
    implementation "relaxngDatatype:relaxngDatatype:20020414"
    implementation "velocity:velocity:1.5"
    implementation "wsdl4j:wsdl4j:1.6.2"
    implementation "xalan:serializer:2.7.1"

    implementation files(localFileDependencies)

    liquibaseRuntime "org.liquibase:liquibase-core:4.7.1"
    liquibaseRuntime "info.picocli:picocli:4.6.3"
    liquibaseRuntime "org.postgresql:postgresql:42.2.20"

    testAnnotationProcessor "org.projectlombok:lombok:1.18.20"
    testCompileOnly "org.projectlombok:lombok:1.18.20"
    testImplementation "jakarta.servlet:jakarta.servlet-api:6.0.0"
    testImplementation "jakarta.servlet.jsp:jakarta.servlet.jsp-api:3.1.1"
    testImplementation "junit:junit:4.11"
    testImplementation "net.sourceforge.htmlunit:htmlunit-core-js:2.70.0"
    testImplementation "net.sourceforge.htmlunit:htmlunit-cssparser:1.14.0"
    testImplementation "net.sourceforge.htmlunit:htmlunit:2.70.0"
    testImplementation "net.sourceforge.htmlunit:neko-htmlunit:2.70.0"
    testImplementation "net.sourceforge.jwebunit:jwebunit-core:2.4"
    testImplementation "net.sourceforge.jwebunit:jwebunit-core:3.3"
    testImplementation "net.sourceforge.jwebunit:jwebunit-htmlunit-plugin:3.3"
    testImplementation "org.eclipse.jetty:jetty-server:11.0.19"
    testImplementation "org.eclipse.jetty:jetty-servlet:11.0.19"
    testImplementation "org.eclipse.jetty:jetty-util:11.0.19"
    testImplementation "org.eclipse.jetty:jetty-webapp:11.0.19"
    testImplementation "org.gebish:geb-core:7.0"
    testImplementation "org.gebish:geb-junit4:7.0"
    testImplementation "org.gebish:geb-spock:7.0"
    testImplementation "org.gebish:geb-testng:7.0"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation "org.mockito:mockito-core:5.0.0"
    testImplementation "org.seleniumhq.selenium:selenium-api:4.16.1"
    testImplementation "org.seleniumhq.selenium:selenium-chrome-driver:4.16.1"
    testImplementation "org.seleniumhq.selenium:selenium-firefox-driver:4.16.1"
    testImplementation "org.seleniumhq.selenium:selenium-support:4.16.1"
    testImplementation "org.spockframework:spock-core:2.3-groovy-4.0"
    testImplementation "org.spockframework:spock-junit4:2.3-groovy-4.0"
    testImplementation "org.spockframework:spock-spring:2.3-groovy-4.0"
}

sourceSets {
    main {
        java {
            srcDirs = ['source']
            destinationDirectory.set(file('home/WEB-INF/classes'))
        }
        resources {
            srcDirs = ['source']
            exclude '**/*.java'
        }
    }
    test {
        java {
            srcDirs = ['test']
            // Exclude test suite files that use unavailable test runners
            exclude '**/SolrUnitTests.java'
            exclude '**/SmokeTestsReadWrite.java'
        }
        resources {
            srcDirs = ['test']
            exclude '**/*.java'
        }
    }
}

test {
//    maxHeapSize = "2048m"
    maxHeapSize = "8g"

//    useJUnitPlatform() //do we want this (junit 4 specific)

    def jvmArgsList = ['--add-opens', 'java.base/jdk.internal.loader=ALL-UNNAMED',
                       '--add-opens', 'java.base/java.util=ALL-UNNAMED',
                       '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
                       '--add-opens', 'java.base/java.lang.invoke=ALL-UNNAMED',
                       '--add-opens', 'java.base/java.util=ALL-UNNAMED',
                       '--add-opens', 'java.prefs/java.util.prefs=ALL-UNNAMED',
                       '--add-opens', 'java.base/java.nio.charset=ALL-UNNAMED',
                       '--add-opens', 'java.base/java.net=ALL-UNNAMED',
                       '--add-opens', 'java.base/java.util.concurrent.atomic=ALL-UNNAMED']
    if (project.hasProperty('gradleDebug')) {
        jvmArgsList.add('-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005')
    }
    jvmArgs(jvmArgsList)
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams = false
    }

    // Check if a specific test suite is requested (from Ant)
    def testSuite = project.hasProperty('suite') ? project.property('suite') : null

    def includeNcbiLoadTests = project.hasProperty('ncbiLoadTests')
    def includeSmokeTests = project.hasProperty('smokeTests')
    def includeNonSmokeTests = project.hasProperty('nonSmokeTests')

    // If a specific suite is requested, ignore other flags
    if (testSuite != null) {
        println "Running test suite: " + testSuite
        includeSmokeTests = false
        includeNonSmokeTests = false
        includeNcbiLoadTests = false
    } else if (!includeSmokeTests && !includeNonSmokeTests) {
        //if neither flag is set, run all tests
        includeSmokeTests = true
        includeNonSmokeTests = true
    }

    doFirst {
        println "Include smoke tests: " + includeSmokeTests
        println "Include non-smoke tests: " + includeNonSmokeTests
        println "Include NCBI load tests: " + includeNcbiLoadTests
    }

    filter {
        if (testSuite != null) {
            // Run specific test suite requested from Ant
            // Suite classes use standard JUnit 4 @RunWith(Suite.class) and can be run directly
            includeTestsMatching "org.zfin." + testSuite
        } else if (includeNcbiLoadTests) {
            includeTestsMatching "org.zfin.datatransfer.ncbi.NCBILoadIntegrationTest"

//   UNCOMMENT BELOW TO RUN CHARACTERIZATION TEST (DANGEROUS)
//            includeTestsMatching "org.zfin.datatransfer.ncbi.NCBILoadCharacterizationTest"

            //don't run other tests
            includeNonSmokeTests = false
            includeSmokeTests = false
        }
        if (includeNonSmokeTests) {
            includeTestsMatching "org.zfin.UnitTests"
            //spock tests

            includeTestsMatching "org.zfin.publication.MeshHeadingSpec"

            includeTestsMatching "org.zfin.publication.presentation.PublicationValidatorSpec"
            includeTestsMatching "org.zfin.publication.repository.PublicationRepositorySpec"
            includeTestsMatching "org.zfin.feature.service.MutationDetailsConversionServiceSpec"
            includeTestsMatching "org.zfin.util.ReportGeneratorSpec"
            includeTestsMatching "org.zfin.search.service.SolrServiceSpec"
            includeTestsMatching "org.zfin.search.service.SolrQueryFacadeSpec"
            includeTestsMatching "org.zfin.search.service.MarkerSearchServiceSpec"
            includeTestsMatching "org.zfin.search.service.SearchSuggestionServiceSpec"

            includeTestsMatching "org.zfin.ontology.service.RibbonServiceIntegrationSpec"
            includeTestsMatching "org.zfin.uniquery.RelatedLinksSpec"

//      Failing tests that we may want to bring back: (ZFIN-8271)
//        includeTestsMatching "org.zfin.uniquery.CategoriesAndFacetsSpec"
//        includeTestsMatching "org.zfin.uniquery.QuerySpec"
//        includeTestsMatching "org.zfin.uniquery.ResultAttributesSpec"

            includeTestsMatching "org.zfin.sequence.repository.SequenceRepositorySpec"
            includeTestsMatching "org.zfin.feature.FeatureServiceSpec"
            includeTestsMatching "org.zfin.curation.service.CurationDTOConversionServiceSpec"
            includeTestsMatching "org.zfin.expression.ExpressionSearchSpec"
            includeTestsMatching "org.zfin.marker.presentation.GeneAddFormBeanValidatorSpec"
            includeTestsMatching "org.zfin.marker.presentation.MarkerGoServiceIntegrationSpec"
            includeTestsMatching "org.zfin.marker.presentation.SequenceTargetingReagentAddBeanValidatorSpec"
            includeTestsMatching "org.zfin.figure.repository.FigureRepositorySpec"
            includeTestsMatching "org.zfin.gbrowse.presentation.GBrowseImageSpec"
            includeTestsMatching "org.zfin.figure.service.ImageServiceSpec"

            /*need to figure out ..all tests fail here
        includeTestsMatching "org.zfin.figure.service.FigureViewServiceSpec"*/

            //includeTestsMatching "org.zfin.publication.PublicationServiceSpec"
            // PublicationServiceSpec > #authorString should bring back #author FAILED
            //org.spockframework.runtime.SpockAssertionError at UnrollIterationNameProvider.java:80


            /*includeTestsMatching "org.zfin.figure.service.ImageServiceSpec" (needs fixing..null pointer here:  tempDir.newFolder(ZfinPropertiesEnum.IMAGE_LOAD.toString(), "medium"))
        includeTestsMatching "org.zfin.figure.service.VideoServiceSpec"*/

            /*//following use geb..likely not to fix

        includeTestsMatching "org.zfin.figure.presentation.FigureViewWebSpec" (geb test)
        includeTestsMatching "org.zfin.marker.MarkerselectWebSpec" (geb test)
        includeTestsMatching "org.zfin.framework.GroovyWebSpec"(geb test)
        includeTestsMatching "org.zfin.ontology.service.RibbonServiceIntegrationSpec"(geb test)*/

            // db unit tests
            includeTestsMatching "org.zfin.DbUnitTests"
            includeTestsMatching "org.zfin.infrastructure.EnumValidationTest"
            includeTestsMatching "org.zfin.DbControllerTests"
            includeTestsMatching "org.zfin.ThirdPartyServiceTests"
        }

        if (includeSmokeTests) {
            includeTestsMatching "org.zfin.anatomy.AnatomySmokeTest"
            includeTestsMatching "org.zfin.antibody.smoketest.AntibodySmokeTest"
            includeTestsMatching "org.zfin.sequence.blast.smoketest.BlastSmokeTest"
            includeTestsMatching "org.zfin.datatransfer.smoketests.DownloadSmokeTest"
            includeTestsMatching "org.zfin.feature.presentation.FeatureDetailSmokeTest"
            includeTestsMatching "org.zfin.feature.presentation.GenotypeDetailSmokeTest"
            includeTestsMatching "org.zfin.expression.presentation.FigureSummarySmokeTest"
            includeTestsMatching "org.zfin.fish.smoketest.FishSmokeTest"
            includeTestsMatching "org.zfin.fish.smoketest.PhenotypeSummarySmokeTest"
            includeTestsMatching "org.zfin.httpunittest.MarkerViewSmokeTest"
            includeTestsMatching "org.zfin.sebservice.MarkerRestSmokeTest"
            //include specific method in any of the tests
            //        includeTestsMatching "*ActiveDataTest"
        }
    }

    reports {
        junitXml.required = true
        html.required = true
    }
}
tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        showExceptions true
        showCauses true
        showStackTraces true

        info.events = debug.events

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

reporting.baseDir = "reports/gradle"
testResultsDirName = "reports/test-results"

task showDirs {
    doLast {
        logger.quiet(rootDir.toPath().relativize(project.reportsDir.toPath()).toString())
        logger.quiet(rootDir.toPath().relativize(project.testResultsDir.toPath()).toString())
    }
}

// ============================================================================
// HELPFUL TASKS
// ============================================================================

// Task to install command-line utilities to TARGETROOT
task installUtilities(type: Copy) {
    dependsOn installDist
    description = 'Install command-line utility scripts to $TARGETROOT/utilities'
    group = 'deployment'

    from layout.buildDirectory.dir('install/zfin-util')
    into "${targetroot}/utilities"

    doLast {
        // Create symlink for easier access
        def binDir = file("${targetroot}/bin")
        binDir.mkdirs()

        println "\n========================================"
        println "Utilities installed successfully!"
        println "========================================"
        println "Location: ${targetroot}/utilities/bin/zfin-util"
        println ""
        println "Usage:"
        println "  zfin-util list              - List available utilities"
        println "  zfin-util help <utility>    - Show help for a utility"
        println "  zfin-util <utility> [args]  - Run a utility"
        println "========================================"
    }
}

// Quick rebuild and reinstall
task reinstallUtilities {
    dependsOn 'classes', 'installUtilities'
    description = 'Rebuild and reinstall command-line utilities'
    group = 'deployment'
}

war {
    archiveFileName = 'zfin.war'

    // Handle duplicates by excluding them (prefer Gradle dependencies)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // Exclude static files served by Apache (not needed in WAR)
    // These files exist in home/ but should not be packaged
    rootSpec.exclude 'robots.txt'
    rootSpec.exclude 'sitemap.xml'
    rootSpec.exclude 'favicon.ico'
    rootSpec.exclude 'zfin_is_down.html'
    rootSpec.exclude 'images/**'
    rootSpec.exclude 'css/**'
    rootSpec.exclude 'javascript/**'
    rootSpec.exclude 'ZFIN/**'
    rootSpec.exclude 'zf_info/**'
    rootSpec.exclude 'ensembl/**'
    rootSpec.exclude 'uniprot/**'
    rootSpec.exclude 'unavailable/**'
    rootSpec.exclude '.DS_Store'

    // Set the web application directory (contains WEB-INF, JSPs, static files)
    webAppDirectory = file('home')

    // Include Hibernate configuration and other conf files in classpath
    from('conf') {
        include 'hibernate.cfg.xml'
        include 'log4j2.xml'
        into 'WEB-INF/classes'
    }

    // Include Hibernate mapping files and properties files from source directory
    from('source') {
        include '**/*.hbm.xml'
        include '**/*.properties'
        into 'WEB-INF/classes'
    }

    // Include asset manifest generated by webpack from TARGETROOT
    from(targetroot + '/home') {
        include 'asset-manifest.json'
        into 'WEB-INF/classes'
    }

    // Compiled classes go into WEB-INF/classes
    // Note: We use pre-compiled classes from home/WEB-INF/classes
    // Source is in 'source/' directory (2,438 Java files + 1,654 resources)
    // To compile from source: gradle compileJava (outputs to home/WEB-INF/classes)

    classpath = configurations.runtimeClasspath

    // Dependencies go into WEB-INF/lib
    // Gradle will automatically package all runtime dependencies
}

// Show dependency tree
task showDependencies {
    doLast {
        configurations.runtimeClasspath.each { println it.name }
    }
}

// Check for conflicts
task checkConflicts {
    doLast {
        println "Checking for dependency conflicts..."
        configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts.each {
            println "${it.moduleVersion.id}"
        }
    }
}

// Clean task
clean {
    delete 'build'
}

// ============================================================================
// ANT COMPILE BRIDGE
// ============================================================================

// Custom compile task that mimics Ant's compile target
// This allows 'ant compile' to delegate to Gradle for compilation
task antCompile {
    description = 'Compile Java sources and copy resources (called by ant compile)'

    dependsOn compileJava

    doLast {
        // Copy XML files from source to classes directory (mimics Ant behavior)
        copy {
            from 'source'
            into 'home/WEB-INF/classes'
            include '**/*.xml'
            exclude '**/.svn'
        }
        println ':JAVA COMPILE SUCCESSFUL:'
    }
}

// Configure compileJava task to output to Ant-compatible location
compileJava {
    destinationDirectory.set(file('home/WEB-INF/classes'))

    // Ensure annotation processing (Lombok) works correctly
    options.annotationProcessorPath = configurations.compileClasspath

    // Java compiler options matching Ant configuration
    options.encoding = 'UTF-8'
    options.debug = true
    options.compilerArgs << '-parameters'
    options.fork = true
    options.forkOptions.memoryMaximumSize = '556m'
}

// ============================================================================
// GWT COMPILATION TASKS
// ============================================================================

// Task to compile a single GWT module
task compileGwtModule(type: JavaExec) {
    description = 'Compile a GWT module (specify with -Pmodule=...)'
    group = 'build'

    def gwtModule = project.hasProperty('module') ? project.property('module') : 'org.zfin.gwt.lookup.Lookup'
    def gwtTarget = project.hasProperty('gwtTarget') ? project.property('gwtTarget') : targetroot + '/home/gwt'

    mainClass = 'com.google.gwt.dev.Compiler'
    classpath = configurations.compileClasspath + files('source')
    args = [
        '-war', gwtTarget,
        '-style', 'DETAILED',
        gwtModule
    ]
    jvmArgs = ['-Xms256m', '-Xmx512m']

    doFirst {
        delete "${gwtTarget}/${gwtModule}"
        println "Compiling GWT module ${gwtModule} to ${gwtTarget}"
    }
}

// Task to compile all GWT modules
task gwtCompile {
    description = 'Compile all GWT modules'
    group = 'build'

    dependsOn 'compileJava'

    doLast {
        def modules = [
            'org.zfin.gwt.lookup.Lookup',
            'org.zfin.gwt.marker.Marker',
            'org.zfin.gwt.curation.Curation'
        ]

        def gwtTarget = targetroot + '/pre-home/gwt'

        // Compile modules sequentially
        modules.each { module ->
            delete "${gwtTarget}/${module}"
            println "Compiling GWT module ${module} to ${gwtTarget}"
            javaexec {
                mainClass = 'com.google.gwt.dev.Compiler'

                // Match the working command line classpath: home/WEB-INF/classes:lib/Java/gwt/*:source
                classpath = files('home/WEB-INF/classes') +
                            fileTree(dir: 'lib/Java/gwt', include: '*.jar') +
                            files('home/WEB-INF/lib/restygwt-2.2.7-eclipse-transformed.jar') +
                            files('source')

                args = [
                    '-war', gwtTarget,
                    '-strict',
                    '-style', 'DETAILED',
                    module
                ]
                jvmArgs = ['-Xms256m', '-Xmx512m']
            }
        }
    }
}


// ============================================================================
// DEVELOPMENT HOT-RELOAD TASKS
// ============================================================================

// Directory where Tomcat unpacks the WAR
def catalinaBase = System.getenv('CATALINA_BASE') ?: '/opt/zfin/catalina_bases/zfin.org'
def deployedClassesDir = catalinaBase + '/webapps/ROOT/WEB-INF/classes'

// Compile and deploy classes to running Tomcat (triggers reload via reloadable="true")
task devCompile {
    description = 'Compile Java and sync to deployed Tomcat for hot reload'
    group = 'development'

    dependsOn compileJava

    doLast {
        // Copy compiled classes to deployed location
        copy {
            from 'home/WEB-INF/classes'
            into deployedClassesDir
            include '**/*.class'
        }
        // Copy resources (XML, properties, etc.)
        copy {
            from 'source'
            into deployedClassesDir
            include '**/*.xml'
            include '**/*.properties'
            exclude '**/*.java'
        }
        println "Classes synced to ${deployedClassesDir} - Tomcat will reload"
    }
}

// Watch for changes and auto-compile (use with: gradle devWatch --continuous)
task devWatch {
    description = 'Watch for source changes and sync to Tomcat (run with --continuous)'
    group = 'development'
    dependsOn devCompile
}
